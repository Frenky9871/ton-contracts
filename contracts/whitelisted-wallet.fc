;; Simple Whitelisted Wallet Contract

;; Parsing destination message
_ check_restricted_destination(msg, wc, dest) inline_ref {
  var cs = msg.begin_parse();
  var flags = cs~load_uint(4);
  if (flags & 8) {
    ;; external messages are never valid
    return false;
  }
  var (s_addr, d_addr) = (cs~load_msg_addr(), cs~load_msg_addr());
  var (dest_wc, dest_addr) = parse_std_addr(d_addr);
  return (dest_wc == wc) & (dest_addr == dest);
}

() recv_internal(slice in_msg) impure {
  ;; do nothing for internal messages
}

() recv_external(slice in_msg) impure {

  ;; Read Data
  var dataSc = begin_parse(get_data());
  var stored_seqno = dataSc~load_uint(32);
  var stored_restricted_key = dataSc~load_uint(256);
  var stored_master_key = dataSc~load_uint(256);
  var restricted_address_wc = dataSc~load_int(8);
  var restricted_address_hash = dataSc~load_uint(256);
  var restricted_last_tx = dataSc~load_uint(32);
  dataSc.end_parse();

  ;; Read Message
  var signature = in_msg~load_bits(512);
  var msgSc = in_msg;
  int msg_seqno = msgSc~load_uint(32);
  
  ;; Check Input
  throw_unless(33, msg_seqno == stored_seqno);
  var data_hash = slice_hash(in_msg);
  var restricted_ok = check_signature(data_hash, signature, stored_restricted_key);
  var master_ok = check_signature(data_hash, signature, stored_master_key);
  throw_unless(34, restricted_ok | master_ok);
  var restrict = ~ master_ok;

  ;; Check restricted
  if (restrict) {
    throw_if(38, now() - restricted_last_tx < 60 * 30);
    restricted_last_tx = now();
    var msgSc2 = msgSc;
    msgSc2~touch();
    throw_unless(37, msgSc2.slice_refs());
    while (msgSc2.slice_refs()) {

      ;; Prohibit weird messages: destruction on zero, send all remaining balance, etc.
      var mode = msgSc2~load_uint(8);
      throw_if(36, mode & 0xfc);

      ;; Check messages
      var msg = msgSc2~load_ref();
      var ok = check_restricted_destination(msg, restricted_address_wc, restricted_address_hash);
      throw_unless(35, ok);
    }
    msgSc2.end_parse();
  }

  ;; Accept Message and spend gas
  accept_message();

  ;; Update Data
  set_data(begin_cell()
    .store_uint(stored_seqno + 1, 32)
    .store_uint(stored_restricted_key, 256)
    .store_uint(stored_master_key, 256)
    .store_int(restricted_address_wc, 8)
    .store_uint(restricted_address_hash, 256)
    .store_uint(restricted_last_tx, 32)
    .end_cell()
  );

  ;; Commit transaction
  commit();

  ;; Send messages
  msgSc~touch();
  while (msgSc.slice_refs()) {
    var mode = msgSc~load_uint(8);
    var msg = msgSc~load_ref();
    send_raw_message(msg, mode);
  }
  msgSc.end_parse();
}

int seqno() method_id {
  return get_data().begin_parse().preload_uint(32);
}